#include <Arduino.h>
#include <WiFi.h>
#include <Firebase_ESP_Client.h>
#include <Wire.h>
#include "time.h"
#include <OneWire.h>
#include <DallasTemperature.h>


// Provide the token generation process info.
#include "addons/TokenHelper.h"
// Provide the RTDB payload printing info and other helper functions.
#include "addons/RTDBHelper.h"

// Insert your network credentials
#define WIFI_SSID "Wael F3"
#define WIFI_PASSWORD "123456789@"

// Insert Firebase project API Key
#define API_KEY "AIzaSyB2dnClTnvjNJ3stn291Sba2asknUebJSo"

// Insert Authorized Email and Corresponding Password
#define USER_EMAIL "mohamed.wael.elsuid@gmail.com"
#define USER_PASSWORD "123456789"

// Insert RTDB URLefine the RTDB URL
#define DATABASE_URL "https://symmetric-hash-352701-default-rtdb.europe-west1.firebasedatabase.app/"

// Define Firebase objects
FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

// Variable to save USER UID
String uid;

// Database main path (to be updated in setup with the user UID)
String databasePath;
// Database child nodes
String tempPath = "/temperature";
String humPath = "/humidity";
String timePath = "/timestamp";

// Ds18B20 Temperature sensor
// GPIO where the DS18B20 is connected to
const int oneWireBus = 4;     
// Setup a oneWire instance to communicate with any OneWire devices
OneWire oneWire(oneWireBus);

// Pass our oneWire reference to Dallas Temperature sensor 
DallasTemperature sensors(&oneWire);

// Parent Node (to be updated in every loop)
String parentPath;

int timestamp;
FirebaseJson json;

// Network time Protocl Server
const char* ntpServer = "pool.ntp.org";

// Capacitive Soil Moisture Sensor Temperature sensor
// GPIO where the Capacitive Soil Moisture Sensor is connected to
int sensorPin = 33;

float temperature;
float humidity;

// Timer variables (send new readings every thirt seconds)
unsigned long sendDataPrevMillis = 0;
unsigned long timerDelay = 30000;





// Initialize WiFi
void initWiFi() {
	WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
	Serial.println("Connecting to WiFi ..");
	while (WiFi.status() != WL_CONNECTED) {
		Serial.print('.');
		delay(1000);
	}
	Serial.print("\nConnected Successfully\nIp is: ");
	Serial.println(WiFi.localIP());
	Serial.println();
}

// Function that gets current epoch time
unsigned long getTime() {
	time_t now;
	struct tm timeinfo;
	if (!getLocalTime(&timeinfo)) {
		// Serial.println("Failed to obtain time");
		return(0);
	}
	time(&now);
	return now;
}

void setup(){
	Serial.begin(115200);

	// Start the DS18B20 sensor
	sensors.begin();

	//start Wifi
	initWiFi();

	//Configure times   
	configTime(0, 0, ntpServer);

	// Assign the api key (required)
	config.api_key = API_KEY;

	// Assign the user sign in credentials
	auth.user.email = USER_EMAIL;
	auth.user.password = USER_PASSWORD;

	// Assign the RTDB URL (required)
	config.database_url = DATABASE_URL;

	Firebase.reconnectWiFi(true);
	fbdo.setResponseSize(4096);

	// Assign the callback function for the long running token generation task */
	config.token_status_callback = tokenStatusCallback; //see addons/TokenHelper.h

	// Assign the maximum retry of token generation
	config.max_token_generation_retry = 5;

	// Initialize the library with the Firebase authen and config
	Firebase.begin(&config, &auth);

	// Getting the user UID might take a few seconds
	Serial.println("Getting User UID");
	while ((auth.token.uid) == "") {
		Serial.print('.');
		delay(1000);
	}
	// Print user UID
	uid = auth.token.uid.c_str();
	Serial.print("User UID: ");
	Serial.println(uid);

	// Update database path
	databasePath = "/UsersData/" + uid + "/readings";
}

void loop(){

	// Send new readings to database
	if (Firebase.ready() && (millis() - sendDataPrevMillis > timerDelay || sendDataPrevMillis == 0)){
		sendDataPrevMillis = millis();

		//Get current timestamp
		timestamp = getTime();
		Serial.print ("time: ");
		Serial.println (timestamp);

		parentPath= databasePath + "/" + String(timestamp);

		humidity = analogRead(sensorPin);
		humidity = (humidity/4095)*100;
		temperature = sensors.getTempCByIndex(0);

		json.set(tempPath.c_str(), String(temperature));
		json.set(humPath.c_str(), String(humidity));
		json.set(timePath, String(timestamp));
		Serial.printf("Set json... %s\n", Firebase.RTDB.setJSON(&fbdo, parentPath.c_str(), &json) ? "ok" : fbdo.errorReason().c_str());

	}
}
